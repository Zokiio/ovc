plugins {
    id 'java'
}

group = 'com.hytale.voicechat'
version = '1.0.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    
    repositories {
        mavenCentral()
        maven { url = 'https://maven.maxhenkel.de/repository/public' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    }
    
    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version
    
    dependencies {
        // Logging
        implementation 'org.slf4j:slf4j-api:2.0.9'
        implementation 'ch.qos.logback:logback-classic:1.4.12'
        
        // Testing
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
    
    test {
        useJUnitPlatform()
    }
}

// Main hytale-plugin module dependencies
dependencies {
    implementation project(':common')
    
    // Hytale API - using official HytaleServer.jar from Hytale installation
    compileOnly files('/Users/zoki/hytale/server/HytaleServer.jar')
    
    // Voice server dependencies
    implementation "de.maxhenkel.opus4j:opus4j:${opus4j_version}"
    implementation "io.netty:netty-all:${netty_version}"
    
    // JSON processing
    implementation "com.google.code.gson:gson:${gson_version}"
}

// Configure JAR to include dependencies (fat JAR)
jar {
    dependsOn(':common:jar')
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('copyToHytale', Copy) {
    dependsOn jar
    from jar.archiveFile
    into '/Users/zoki/hytale/server/mods'
}

tasks.build {
    finalizedBy copyToHytale
}
